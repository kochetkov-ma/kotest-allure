plugins {
    id 'idea'
    id 'signing'
    id 'maven-publish'

    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'io.qameta.allure' version "$allurePluginVersion"
    id 'io.codearte.nexus-staging' version "$nexusStagingPluginVersion"
    id 'com.github.ben-manes.versions' version "$benManesPluginVersion"
    id 'ru.iopump.qa.allure' version "$allureServerPluginVersion"
}
group 'ru.iopump.kotest'

apply from: "$rootDir/gradle/publishing.gradle"
idea { module { downloadJavadoc = true; downloadSources = true } }
compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}
wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "$gradleWrapperVersion"
    doLast { delete "$projectDir/gradlew.bat", "$projectDir/gradlew" }
}
repositories { mavenLocal(); mavenCentral() }
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation("io.kotest:kotest-framework-api:$kotestVersion") { exclude group: 'org.jetbrains.kotlin' }

    api "org.opentest4j:opentest4j:$opentest4jVersion"
    api "io.qameta.allure:allure-java-commons:$allureVersion"

    testImplementation "io.kotest:kotest-property:$kotestVersion"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
    testImplementation("io.kotest:kotest-runner-junit5-jvm:$kotestVersion") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'io.mockk'
    }
}
test {
    systemProperty "allure.results.directory", file("$buildDir/allure-results")
    useJUnitPlatform()
}
allure { autoconfigure = false; aspectjweaver = true; version = allureVersion; aspectjVersion = aspectJVersion }
dependencyUpdates {
    gradleReleaseChannel = 'current'
}
task cleanAllureResult {
    doLast {
        delete "$buildDir/allure-results"
    }
}
allureServer {
    relativeResultDir = 'build/allure-results'
    allureServerUrl = 'https://allure.iopump.ru'
}